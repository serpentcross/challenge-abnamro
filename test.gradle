plugins {
	id "org.springframework.boot" version "3.2.3"
	id "io.spring.dependency-management" version "1.0.11.RELEASE"
	id "java"
	id "org.openapi.generator" version "6.0.0"
}

group = 'com.cooking'
version = 'v1'
sourceCompatibility = '17'

repositories {
	mavenCentral()
}

def h2Version = "2.3.232"
def jacksonDatabindNullableVersion = "0.2.6"
def liquibaseVersion = "4.29.1"
def lombokVersion = "1.18.34"
def mapStructVersion = "1.6.0.Beta1"
def postgreSqlVersion = "42.7.3"
def snakeYmlVersion = "2.2"
def springVersion = "3.3.2"
def springDocVersion = "2.6.0"

ext {

	set('h2-database.version', '2.2.224')
	set('openapi-generator-maven-plugin.version', '7.4.0')
}

dependencies {
	implementation("org.springframework.boot:spring-boot-starter-web:${springVersion}") {
		exclude module: 'spring-boot-starter-tomcat'
	}
	implementation("org.springframework.boot:spring-boot-starter-undertow:${springVersion}")
	implementation("org.springframework.boot:spring-boot-devtools:${springVersion}")
	implementation("org.springframework.boot:spring-boot-starter-data-jpa:${springVersion}")
	implementation("org.springframework.boot:spring-boot-starter-validation:${springVersion}")

	implementation("org.postgresql:postgresql:${postgreSqlVersion}")
	implementation("org.liquibase:liquibase-core:${liquibaseVersion}")

	implementation("com.h2database:h2:${h2Version}")

	implementation("org.springdoc:springdoc-openapi-starter-webmvc-ui:${springDocVersion}")
	implementation("org.openapitools:jackson-databind-nullable:${jacksonDatabindNullableVersion}")
	implementation("org.yaml:snakeyaml:${snakeYmlVersion}")

	compileOnly("org.projectlombok:lombok:${lombokVersion}")
	annotationProcessor("org.projectlombok:lombok:${lombokVersion}")

	implementation("org.mapstruct:mapstruct:${mapStructVersion}")
	annotationProcessor("org.mapstruct:mapstruct-processor:${mapStructVersion}")

	testImplementation("org.springframework.boot:spring-boot-starter-test:${springVersion}")
	testCompileOnly("org.projectlombok:lombok:${lombokVersion}")
	testAnnotationProcessor("org.projectlombok:lombok:${lombokVersion}")

}

dependencyManagement {
	imports {
		mavenBom "org.springframework.boot:spring-boot-dependencies:${springVersion}"
	}
}

compileJava {
	options.annotationProcessorPath = configurations.annotationProcessor
}

tasks.withType(JavaCompile) {
	options.compilerArgs += ["-parameters"]
}

bootJar {
	enabled = false
}

jar {
	enabled = true
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

test {
	useJUnitPlatform()
	exclude '**/*IntegrationTest.*'
}

sourceSets {
	main {
		java {
			srcDir 'src/main/java'
			srcDir 'build/src/main/java'
		}
	}
}

openApiGenerate {
	ext.sourcesDir = "build/src/main/java"
	generatorName = "spring"
	inputSpec = "${rootDir}/src/main/resources/open-api/cooking-api.yml"
	outputDir = "${buildDir}"
	apiPackage = "com.cooking.api.controllers"
	invokerPackage = "com.cooking.api"
	modelPackage = "com.cooking.api.dtos"
	configOptions = [
		dateLibrary: "java8",
		additionalModelTypeAnnotations: "@lombok.Data;@lombok.Builder;@lombok.NoArgsConstructor;@lombok.AllArgsConstructor",
		interfaceOnly: "true",
		skipDefaultInterface: "true",
		generatePom: "false",
		useJakartaEe: "true"
	]
}
